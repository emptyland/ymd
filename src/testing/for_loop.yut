ForLoopTest = @{
	benchmark : 3000000,

	test_forstep_sanity : func (self) {
		var c = 0, k = 10000
		for var i = 0, k {
			c = c + 1
		}
		// Make sure `i' is local variable
		Assert:Nil(__g__.i)
		Assert:EQ(k, i)
		Assert:EQ(k, c)

		for i = 0, k {
			if i == 1000 {
				break
			}
		} fail {
			Assert:EQ(1000, i)
			return
		}
		Assert:Fail("For loop fail, can not go to here.")
	},

	test_foreach_sanity : func (self) {
		var c = 0, k = 10000
		for var i in range(0, k) {
			c = c + 1
		}
		// Make sure `i' is local variable
		Assert:Nil(__g__.i)
		Assert:EQ(k - 1, i)
		Assert:EQ(k, c)

		for i in range(0, k) {
			if i == 1000 {
				break
			}
		} fail {
			Assert:EQ(1000, i)
			return
		}
		Assert:Fail("For loop fail, can not go to here.")
	},

	test_forstep_step : func (self) {
		var i, c = 0
		for i = 0, 100, 2 {
			c = c + 1
		}
		Assert:EQ(50, c)
		Assert:EQ(100, i)

		c = 0
		for i = 0, 3, 2 {
			c = c + 1
		}
		Assert:EQ(4, i)
		Assert:EQ(2, c)

		c = 0
		for i = 3, 0, -2 {
			c = c + 1
		}
		Assert:EQ(-1, i)
		Assert:EQ(2, c)

		c = 0
		var a = [0, 1, 2, 3, 4, 5, 6]
		for i = 0, len(a) {
			Assert:EQ(a[i], i)
			c = c + 1
		}
		Assert:EQ(7, i)
		Assert:EQ(7, c)
	},

	test_forstep_once : func (self) {
		var n = 0
		var func initial() {
			var r = n
			n = n + 1
			return r
		}
		n = 3
		var func stop() {
			var r = n
			n = n + 1
			return r
		}
		n = 1
		var func step() {
			var r = n
			n = n + 1
			return r
		}
		var c = 0
		// like: for var i = 0, 3, 1
		for var i = initial(), stop(), step() {
			c = c + 1
		}
		Assert:EQ(3, i)
		Assert:EQ(3, c)
	},

	test_foreach_benchmark : func (self) {
		for var i in range(0, self.benchmark) {
		}
	},

	test_foreach_benchmark_user : func (self) {
		var func range(i, end) {
			return func () {
				if i > end {
					return nil
				}
				i = i + 1
				return i
			}
		}
		for var i in range(0, self.benchmark) {
		}
	},

	test_forstep_benchmark : func (self) {
		var i
		for i = 0, self.benchmark {
		}
	},

	test_fail_sanity : func (self) {
		for {
			break
		} fail {
			return nil
		}
		Assert:Fail("Can not goto here, `fail' not work!")
	},

	test_fail_range : func (self) {
		var k = 1000
		for var i = 0, 10000 {
			if i == k {
				break
			}
		} fail {
			Assert:EQ(k, i)
			return nil
		}
		Assert:Fail("Can not goto here, `fail' not work!")
	},

	test_range_counting : func (self) {
		var c = 0 // counter
		var k = 65535 // max number
		for var i = 0, k {
			c = c + 1
			Assert:GT(k, i)
		} fail {
			Assert:Fail("Can not goto here.")
		}
		Assert:EQ(k, c)
	},

	test_fail_range_negative : func (self) {
		for var i = 0, 100 {

		} fail {
			Assert:Fail("Can not goto here, not fail.")
		}
	}
}
