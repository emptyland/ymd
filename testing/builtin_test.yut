BuiltinTest = @{
	test00len : func (self) {
		Assert:EQ(0, len(""))
		Assert:EQ(1, len("0"))
		Assert:EQ(2, len("\n\r"))

		Assert:EQ(0, len(nil))
		Assert:EQ(0, len([]))
		Assert:EQ(0, len({}))
		Assert:EQ(0, len(@{}))

		var val = [0, 1, 2, 3, 4, 5, 6, 7, 8, 9, 10, 11, 12]
		Assert:EQ(13, len(val))
		insert(val, 13)
		Assert:EQ(14, len(val))

		val = {[0] = 0, [1] = 1, [2] = 2, [3] = 3, [4] = 4,
			[5] = 5, [6] = 6, [7] = 7, [8] = 8, [9] = 9,
			[10] = 10, [11] = 11, [12] = 12}
		Assert:EQ(13, len(val))
		insert(val, 13, 13)
		Assert:EQ(14, len(val))

		val = @{[0] = 0, [1] = 1, [2] = 2, [3] = 3, [4] = 4,
			[5] = 5, [6] = 6, [7] = 7, [8] = 8, [9] = 9,
			[10] = 10, [11] = 11, [12] = 12}
		Assert:EQ(13, len(val))
		insert(val, 13, 13)
		Assert:EQ(14, len(val))
	},

	test01insert : func (self) {
		var val = []
		Assert:EQ(0, len(val))

		insert(val, 0)
		Assert:EQ(1, len(val))
		Assert:EQ(0, val[0])

		insert(val, false)
		Assert:EQ(2, len(val))
		Assert:EQ(false, val[1])

		insert(val, "Element")
		Assert:EQ(3, len(val))
		Assert:EQ("Element", val[2])

		insert(val, func () {})
		Assert:EQ(4, len(val))
		Assert:EQ("function", typeof val[3])

		insert(val, [])
		Assert:EQ(5, len(val))
		Assert:EQ([], val[4])

		insert(val, {})
		Assert:EQ(6, len(val))
		Assert:EQ({}, val[5])

		insert(val, @{})
		Assert:EQ(7, len(val))
		Assert:EQ(@{}, val[6])
	},

	test02insert_ordered : func (self) {
		var val = [0, 1, 2, 3, 4, 5, 6, 7]
		var i = len(val);
		Assert:EQ(0, val[0])
		Assert:EQ(1, val[1])
		Assert:EQ(i, len(val))

		// Insert to [0]
		// "a", 0, 1, 2, ...
		insert(val, 0, "a")
		Assert:EQ("a", val[0])
		Assert:EQ(0, val[1])
		Assert:EQ(1, val[2])
		Assert:EQ(i + 1, len(val))

		// Insert to [2]
		// "a", 0, "b", 1, 2, ...
		insert(val, 2, "b")
		Assert:EQ(0, val[1])
		Assert:EQ("b", val[2])
		Assert:EQ(1, val[3])
	},

	test03append : func (self) {
		var val = []
		append(val, 0, 1, 2, 3)
		Assert:EQ(4, len(val))
		Assert:EQ(0, val[0])
		Assert:EQ(1, val[1])
		Assert:EQ(2, val[2])
		Assert:EQ(3, val[3])

		append(val, 0)
		Assert:EQ(5, len(val))
		Assert:EQ(0, val[4])
	},

	test04range : func (self) {
		var iter = range(0, 0)
		Assert:Nil(iter())
		Assert:Nil(iter()) // Again

		iter = range(0, 1)
		Assert:EQ(0, iter())
		Assert:Nil(iter())

		iter = range(100, 102)
		Assert:EQ(100, iter())
		Assert:EQ(101, iter())
		Assert:Nil(iter())

		iter = range(1, 0)
		Assert:EQ(1, iter())
		Assert:Nil(iter())

		// Reserved range iterator:
		iter = range(4, 0, -2)
		Assert:EQ(4, iter())
		Assert:EQ(2, iter())
		Assert:Nil(iter())

		iter = range(0, 6, 2)
		Assert:EQ(0, iter())
		Assert:EQ(2, iter())
		Assert:EQ(4, iter())
		Assert:Nil(iter())
	},

	test05range_container : func (self) {
		var iter = range([])
		var i
		for i in range(0, rand(100, 200)) {
			Assert:Nil(iter())
		}

		iter = range([0, "1", false, func () {}, [], {}, @{}])
		Assert:EQ(0, iter())
		Assert:EQ("1", iter())
		Assert:EQ(false, iter())
		Assert:EQ("function", typeof iter())
		Assert:EQ([], iter())
		Assert:EQ({}, iter())
		Assert:EQ(@{}, iter())
		Assert:Nil(iter())

		iter = range({a:0, b:1, c:2})
		Assert:EQ(0, iter())
		Assert:EQ(1, iter())
		Assert:EQ(2, iter())
		Assert:Nil(iter())

		iter = range(@{a:0, b:1, c:2})
		Assert:EQ(0, iter())
		Assert:EQ(1, iter())
		Assert:EQ(2, iter())
		Assert:Nil(iter())
	},

	test06rank : func (self) {
		var iter = rank([])
		Assert:Nil(iter())
		Assert:Nil(iter())

		iter = rank([0, 1, 2])
		Assert:EQ([0, 0], iter())
		Assert:EQ([1, 1], iter())
		Assert:EQ([2, 2], iter())
		Assert:Nil(iter())

		iter = rank({a:0, b:1, c:2})
		Assert:EQ(["a", 0], iter())
		Assert:EQ(["b", 1], iter())
		Assert:EQ(["c", 2], iter())
		Assert:Nil(iter())

		iter = rank(@{a:0, b:1, c:2})
		Assert:EQ(["a", 0], iter())
		Assert:EQ(["b", 1], iter())
		Assert:EQ(["c", 2], iter())
		Assert:Nil(iter())
	},

	test07import : func (self) {
		var lib = import "testing/imported1.ymd"
		Assert:EQ("imported1", lib)
		lib = import "testing/imported2.ymd"
		Assert:EQ("imported2", lib)
		print(__reached__)

		lib = import "testing/imported1.ymd"
		Assert:Nil(lib)
		lib = import "testing/imported2.ymd"
		Assert:Nil(lib)
		print(__reached__)
	}
}
